MODULE OrderedDictionaryTest;

   IMPORT Out,
	 T := OrderedDictionary,
	 I := Interfaces;

   CONST
      maxn = 100;

   TYPE
      IntKey = POINTER TO IntKeyDesc;
      IntKeyDesc = RECORD (I.ObjDesc)
	 k : INTEGER
      END;

      IntVal = POINTER TO IntValDesc;
      IntValDesc = RECORD (I.ObjDesc)
	 v : INTEGER
      END;

   VAR
      d : T.DictDesc;
      compareNodes : T.KeyComparerTCDesc;
      i : INTEGER;
      k : IntKey;
      v : IntVal;


   PROCEDURE Less(kc : T.KeyComparerTC; a, b : I.Obj) : BOOLEAN;
   BEGIN
      RETURN a(IntKey).k < b(IntKey).k
   END Less;


   PROCEDURE printer(ok, ov : I.Obj; state : I.Obj) : BOOLEAN;
      VAR k : IntKey;
	 v : IntVal;
   BEGIN
      k := ok(IntKey);
      v := ov(IntVal);
      IF (k.k >= 10) & (k.k <= 20) THEN 
	 Out.Int(k.k, 0); Out.String(" ");
      END;
      RETURN k.k < 20
   END printer;

BEGIN
   compareNodes.Less := Less;
   T.Init(d, compareNodes);

   Out.String("Inserting keys"); Out.Ln;
   FOR i := 1 TO maxn DO
      NEW(k); k.k := i;
      NEW(v); v.v := -i;
      T.Insert(d, k, v);
   END;

   Out.String("Checking keys, ct = "); Out.Int(T.Count(d), 0); Out.Ln;
   NEW(k);
   FOR i := 1 TO maxn DO
      k.k := i;
      IF ~T.ContainsKey(d, k) THEN
	 Out.String("Missing key "); Out.Int(i, 0); Out.Ln;
      END;
   END;

   Out.String("Removing odd keys"); Out.Ln;
   FOR i := 1 TO maxn BY 2 DO
      k.k := i;
      T.DeleteKey(d, k);
   END;

   Out.String("Verifying keys were deleted, ct = "); Out.Int(T.Count(d), 0); Out.Ln;
   FOR i := 1 TO maxn BY 2 DO
      k.k := i;
      IF T.ContainsKey(d, k) THEN
	 Out.String("Key should have been deleted"); Out.Int(i, 0); Out.Ln;
      END;
   END;

   Out.String("Verifying other keys remain"); Out.Ln;
   FOR i := 2 TO maxn BY 2 DO
      k.k := i;
      IF ~T.ContainsKey(d, k) THEN
	 Out.String("Key should not have been deleted"); Out.Int(i, 0); Out.Ln;
      END;
   END;


   Out.String("Printing keys from 10 to 20"); Out.Ln;
   T.Visit(d, printer, NIL);

   Out.String("Done"); Out.Ln;
END OrderedDictionaryTest.
